module Monitor;

export *;

import * from ABS.DC;
import * from Architecture;
import * from Param;
import * from Migration;


interface IMonitor {}

class Monitor(Migration edge, Migration cloud, String z, IStorage storage) implements IMonitor {

    String zone = z;
    Int times = 0;
    Int lat_time_series_curr_index = 0;
    Int size_time_series_curr_index = 0;
    Int i = 0;

    Unit run() {
        while(True) {
            await duration(monitoring_inverval(), monitoring_inverval());
            List<Rat> latencies = storage.get_latency_timeserie();
            List<Rat> sizes = storage.get_size_timeserie();
            Rat avg_lat = this.compute_average(latencies, lat_time_series_curr_index);
            Rat avg_size = this.compute_average(sizes, size_time_series_curr_index);
            lat_time_series_curr_index = length(latencies);
            size_time_series_curr_index = length(sizes);
            Bool areNewRequestsAvailable = storage.areNewRequestsAvailabible();
            if(areNewRequestsAvailable) {
                if((avg_lat > max_lat_threshold() || avg_lat < 0) && zone == "cloud") this!migrate();
                else if(avg_lat < min_lat_threshold() && avg_lat > 0 && zone == "edge") this!migrate();
            }
            this.log(now(), avg_lat, avg_size, zone, areNewRequestsAvailable);
            i = i + 1;
        }
    }

    Unit migrate() {
       if(zone == "cloud") {
            zone = "edge";
            cloud!undeploy();
            edge!deploy();
        } else {
            zone = "cloud";  
            edge!undeploy();
            cloud!deploy();
        }
    }

    Rat compute_average(List<Rat> values, Int curr_index) {
        Rat tmp = 0;
        Int i = curr_index;
        while(i < length(values)) {
            tmp = tmp + nth(values, i);
            i = i + 1;
        }
        if(curr_index == i) tmp = -1;
        else tmp = tmp / (length(values) - curr_index);
        return tmp;
    }

    Unit log(Time time, Rat avg_latency, Rat avg_size, String zone, Bool areNewRequestsAvailable) {
        Float sec_elapsed = float(timeValue(time) / sec_to_time_unit());
        Float avg_size_mb = float(avg_size / pow(10,6));
        Float avg_lat_ms = float(1000 * avg_latency / sec_to_time_unit());
        String toPrint = "[" + toString(sec_elapsed) + " sec]";
        if(areNewRequestsAvailable) {
            if(avg_latency >= 0) toPrint = toPrint + " [avg lat: "  + toString(avg_lat_ms) + " ms]";
            else toPrint = toPrint + " [avg lat: -1 ms]";
            if(avg_size >= 0) toPrint = toPrint + " [avg size: " + toString(avg_size_mb) + " Mb]";
            else toPrint = toPrint + " [avg size: -1 Mb]";
        } else toPrint = toPrint + " [No Requests Found]";
        toPrint = toPrint + " [zone: " + zone + "]";
        println(toPrint);
    }
}